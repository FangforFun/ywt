apply plugin: 'com.android.application'
apply plugin: 'android-apt'

//获取时间戳
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}

android {
    compileSdkVersion 22  
    buildToolsVersion "22.0.1"


    defaultConfig {
        applicationId "com.gkzxhn.gkprison"
        minSdkVersion 17
        targetSdkVersion 21
        versionCode 6
        versionName "1.2.0"
        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
    }

    dexOptions{
        preDexLibraries = false
        incremental true
        javaMaxHeapSize "4g"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        wandoujia {}
//        baidu {}
//        c360 {}
//        uc {}
        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }

    //修改生成的apk名字
    applicationVariants. all{ variant->
        variant.outputs.each { output->
            def oldFile = output.outputFile
            def newName = '';
            if (variant.buildType.name.equals('release')) {
                def releaseApkName = 'yuwutong-' + defaultConfig.versionName + '.apk'
                output.outputFile = new File(oldFile.parent, releaseApkName)
            }
            if (variant.buildType.name.equals('beta')) {
                newName = oldFile.name.replace(".apk", "-v" + defaultConfig.versionName + "-build" + getDate() + ".apk")
                output.outputFile = new File(oldFile.parent, newName)
            }
            if (variant.buildType.name.equals('debug')) {

            }
        }
    }

    afterEvaluate {
        tasks.matching {
            it.name.startsWith('dex')
        }.each { dx ->
            def listFile = project.rootDir.absolutePath+'/app/maindexlist.txt'
            if (dx.additionalParameters == null) {
                dx.additionalParameters = []
            }
            //表示当方法数越界时则生成多个dex文件
            dx.additionalParameters += '--multi-dex'
            //这个指定了listFile中的类（即maindexlist.txt中的类）会打包到主dex中，不过注意下一条。
            dx.additionalParameters += '--main-dex-list=' +listFile
            //表明只有-main-dex-list所指定的类（在我的配置中，就是app目录下的maindexlist.txt中包含的类）才能打包到主dex中，如果没有这个选项，上个选项就会失效
            dx.additionalParameters += '--minimal-main-dex'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':libraries:ToggleButtonLib')
    compile project(':libraries:uikit')
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile 'com.klinkerapps:link_builder:1.3.1@aar'
    compile 'de.greenrobot:eventbus:2.1.0'
    compile 'com.jungly:gridPasswordView:0.3'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'me.biubiubiu.justifytext:library:1.1'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'com.android.support:multidex:1.0.1'
    compile project(':libraries:livenessLib')
}
